#===============================================================================
# Initial setup to define the main output folders and log file:
# - handle Linux and Windows for testing and production environments
# - see the next section for an explanation of the workflow
#===============================================================================
# Open the log file:
# - the 'results' folder may always be used, such as for Excel time series catalog
# - if running on Windows, assume development and output the log and data loader files to the 'results' folder
# - if running on Linux (even Git Bash or other variant on a Windows computer),
#   save to /var/log/systems/PA-Commission-SRBC folder to test the Linux output
CreateFolder(Folder="results",IfFolderExists="Ignore")
SetProperty(PropertyName="LinuxOS",PropertyType="Boolean",PropertyValue="False")
If(Name="If_Linux_Log",FileExists="/dev/null")
    Message(Message="Running on Linux.",CommandStatus="NOTIFICATION")
    SetProperty(PropertyName="LogFile",PropertyType="String",PropertyValue="/var/log/systems/PA-Commission-SRBC/download-campbell-cloud-data.tstool.log")
    CreateFolder(Folder="/var/log/systems/PA-Commission-SRBC",CreateParentFolders="True",IfFolderExists="Ignore")
    SetProperty(PropertyName="LinuxOS",PropertyType="Boolean",PropertyValue="True")
EndIf(Name="If_Linux_Log")
If(Name="If_Windows_Log",Condition="${LinuxOS} == False")
    # Windows:
    # - check for Linux above first, which will evaluate to true for Git Bash and other Linux variants
    Message(Message="Running on Windows.",CommandStatus="NOTIFICATION")
    SetProperty(PropertyName="WindowsOS",PropertyType="Boolean",PropertyValue="True")
    SetProperty(PropertyName="LogFile",PropertyType="String",PropertyValue="results/download-campbell-cloud-data.tstool.log")
EndIf(Name="If_Windows_Log")
StartLog(LogFile="${LogFile}")
#===============================================================================
# Workflow to read PA-Commission-SRBC data from Campbell Cloud and hand off to the NovaStar dataloader:
# - station list is initially read from Campbell cloud but may read from NovaStar in the future
# - the workflow is expected to be run as a NovaStar scheduled process every 5 minutes offset 1 minute from
#   5 minutes to allow Campbell Cloud to upload
# - to help avoid minor issues with data availability, read 3 hours of data
#   (NovaStar will ignore refiled data)
# - round the InputEnd to 5 minutes forward
# - the different Campbell Cloud stations report data at different intervals
#   (e.g., RangeVUE is less frequent regular reports if no change in data)
#
#@version 1.0.3
#@versionDate 2025-12-03
#
# History:
# 1.0.3, 2025-12-03 - Add more error handling for more possible cases.
#                   - Generalize a problems file to insert in the output (concept can also work for other workflows).
# 1.0.2, 2025-11-29 - Remove DateTime column since no longer needed.
#                     Add comments to the header for time series with no data.
# 1.0.1, 2025-11-26 - Add DateTime column to the dataloader file to work with current dataloader.
# 1.0.0, 2025-11-24 - Initial version.
#===============================================================================
# General Setup:
# - define properties to control the workflow
#===============================================================================
# Controlling properties:
# - 'CurrentTime' = current date/time for logging
# - 'DataLoaderTime' = current date/time for data loader CSV file name
# - 'DoCreateDataloaderFile' = true if output file should be created for the NovaStar dataloader
# - 'DoCreateExcelTSCatalog' = set to true to output an Excel time series catalog, used to help with system setup
# - 'ProblemCount' = increment to indicate that the problems file has messages
SetProperty(PropertyName="CurrentTime",PropertyType="DateTime",PropertyValue="CurrentToSecond")
FormatDateTimeProperty(PropertyName="DataLoaderTime",DateTimePropertyName="CurrentTime",Format="%Y%m%d%H%M")
SetProperty(PropertyName="DoCreateDataloaderFile",PropertyType="Boolean",PropertyValue="True")
SetProperty(PropertyName="DoCreateExcelTSCatalog",PropertyType="Boolean",PropertyValue="False")
SetProperty(PropertyName="ProblemCount",PropertyType="Integer",PropertyValue="0")
#
# Remove the problem file for this workflow.
RemoveFile(InputFile="${MissingTimeSeriesFile}",IfNotFound="Ignore")
#
# Set the input period to:
# - 'InputEnd' = rounded to 5 minutes in the future
# - 'InputStart' = 'InputEnd' minus 3 hours (can adjust based on production system behavior)
#                  Use longer than 3 hours during startup to test while stations are offline for transport.
SetInputPeriod(InputStart="CurrentToSecond.Round(5Min).RoundDirection(>) - 3Hour",InputEnd="CurrentToSecond.Round(5Min).RoundDirection(>)")
#
# Set properties based on whether RunMode is Development or Production:
# - 'DataloaderFolder' = where to creat the output file from Campbell Cloud data download
# - 'OutputFolder' = where to create other local output files (not log and not dataloader files)
# - 'ProblemsFile' = file for specific problems, which will be inserted into the header template to replace INSERT_PROBLEMS_HERE
SetProperty(PropertyName="DataloaderFolder",PropertyType="String",PropertyValue="/usr/ns/dataloader")
SetProperty(PropertyName="OutputFolder",PropertyType="String",PropertyValue="results")
SetProperty(PropertyName="ProblemsFile",PropertyType="String",PropertyValue="${OutputFolder}/${DataLoaderTime}-SRBC-CampbellCloud-problems.txt")
If(Name="If_Dataloader_Linux",Condition="${LinuxOS} == False")
    # Write the dataloader file to the 'results' folder.
    SetProperty(PropertyName="DataloaderFolder",PropertyType="String",PropertyValue="results")
EndIf(Name="If_Dataloader_Linux")
#===============================================================================
# Define an in-memory table to map Campbell Cloud datastream field to NovaStar data type:
# - this expects SHEF parameter code to be used in NovaStar
#===============================================================================
# Create an in-memory table to lookup NovaStar data type from Campbell Cloud data type, which maps
# - Campbell Cloud datastram field
# - NovaStar data type (use the SHEF parameter code since the dataloader can handle easier)
NewTable(TableID="CampbellCloudToNovaStarDataTypeTable",Columns="CCDataType,string;NSDataType,string")
InsertTableRow(TableID="CampbellCloudToNovaStarDataTypeTable",ColumnValues="CCDataType:AirTemp,NSDataType:TA")
InsertTableRow(TableID="CampbellCloudToNovaStarDataTypeTable",ColumnValues="CCDataType:BattVoltage,NSDataType:VB")
InsertTableRow(TableID="CampbellCloudToNovaStarDataTypeTable",ColumnValues="CCDataType:BP,NSDataType:PA")
InsertTableRow(TableID="CampbellCloudToNovaStarDataTypeTable",ColumnValues="CCDataType:Rain,NSDataType:PP")
InsertTableRow(TableID="CampbellCloudToNovaStarDataTypeTable",ColumnValues="CCDataType:RH,NSDataType:XR")
InsertTableRow(TableID="CampbellCloudToNovaStarDataTypeTable",ColumnValues="CCDataType:Solar,NSDataType:RW")
InsertTableRow(TableID="CampbellCloudToNovaStarDataTypeTable",ColumnValues="CCDataType:Stage,NSDataType:HG")
InsertTableRow(TableID="CampbellCloudToNovaStarDataTypeTable",ColumnValues="CCDataType:TotalAccumulation,NSDataType:PC")
InsertTableRow(TableID="CampbellCloudToNovaStarDataTypeTable",ColumnValues="CCDataType:WindDir,NSDataType:UD")
InsertTableRow(TableID="CampbellCloudToNovaStarDataTypeTable",ColumnValues="CCDataType:WindSpd,NSDataType:US")
InsertTableRow(TableID="CampbellCloudToNovaStarDataTypeTable",ColumnValues="CCDataType:WindSpdMax,NSDataType:UP")
#===============================================================================
# Read Campbell Cloud time series catalog and stations.
#===============================================================================
# Read the full Campbell Cloud time series catalog:
# - only read the metadata (ReadTimeSeries=False)
# - this table is used to determine other data as needed
# - write it to Excel temporarily to help with system setup
ReadCampbellCloud(DataStore="CampbellCloud-SRBC",DataType="*",Interval="*",ReadTimeSeries="False",TimeSeriesCatalogTableID="CCTimeSeriesCatalogTable")
SetPropertyFromTable(TableID="CCTimeSeriesCatalogTable",RowCountProperty="CCTimeSeriesCatalogTableRowCount")
If(Name="If_CCTimeSeriesCatalogEmpty",Condition="${CCTimeSeriesCatalogTableRowCount} == 0")
    # There is probably a problem with the Campbell Cloud datastore configuration or it is offline.
    SetProperty(PropertyName="HaveError",PropertyType="Boolean",PropertyValue="True")
    AppendFile(InputFile="${ProblemsFile}",AppendText="#  The Campbell Cloud time series catalog has zero time series (problem with datastore?).",OutputFile="${ProblemsFile}",IfNotFound="Ignore")
    SetProperty(PropertyName="ProblemCount",PropertyType="Integer",PropertyValue="${ProblemCount}",Add="1")
EndIf(Name="If_CCTimeSeriesCatalogEmpty")
#
# Write the time series catalog to Excel:
# - do it even if the table is zero size
If(Name="If_DoCreateExcelTSCatalog",Condition="${DoCreateExcelTSCatalog} == true")
    NewExcelWorkbook(OutputFile="${OutputFolder}/download-campbell-cloud-data-tscatalog.xlsx",Worksheets="tscatalog",KeepOpen="True")
    WriteTableToExcel(TableID="CCTimeSeriesCatalogTable",OutputFile="${OutputFolder}/download-campbell-cloud-data-tscatalog.xlsx",Worksheet="tscatalog",ExcelAddress="A1",ExcelColumnNames="FirstRowInRange",ColumnWidths="*:Auto")
EndIf(Name="If_DoCreateExcelTSCatalog")
#
# Create a table containing the unique list of Campbell Cloud stations, with asset information:
# - this is used to iterate below when requesting time series
CopyTable(TableID="CCTimeSeriesCatalogTable",NewTableID="CCStationsTable",IncludeColumns="StationId,StationName,Assetid,AssetDescription",DistinctColumns="StationId,StationName,Assetid,AssetDescription",RowCountProperty="CCStationsTableRowCount")
#
# Sort the stations by station name
SortTable(TableID="CCStationsTable",SortColumns="StationName")
#===============================================================================
# Read NovaStar time series catalog and stations.
#===============================================================================
# Read the NovaSTar time series catalog for scaled value time series:
# - this is used to look up the NovaStar stationNumId from Campbell Cloud station ID stored in remote tag name
# - also create a table listing unique NovaStar stations with remote tag name (Campbell Cloud station ID)
ReadNovaStar(DataStore="nsdataws-nscloud",DataType="*",Interval="*",Where1="IncludeScaledTS;Matches;Only",ReadTimeSeries="False",TimeSeriesCatalogTableID="NSTimeSeriesCatalogTable")
SetPropertyFromTable(TableID="NSTimeSeriesCatalogTable",RowCountProperty="NSTimeSeriesCatalogTableRowCount")
If(Name="If_NSTimeSeriesCatalogEmpty",Condition="${NSTimeSeriesCatalogTableRowCount} == 0")
    # There is probably a problem with the NovaStar datastore configuration or it is offline.
    SetProperty(PropertyName="HaveError",PropertyType="Boolean",PropertyValue="True")
    AppendFile(InputFile="${ProblemsFile}",AppendText="#  The Campbell Cloud time series catalog has zero time series (problem with datastore?).",OutputFile="${ProblemsFile}",IfNotFound="Ignore")
    SetProperty(PropertyName="ProblemCount",PropertyType="Integer",PropertyValue="${ProblemCount}",Add="1")
EndIf(Name="If_NSTimeSeriesCatalogEmpty")
CopyTable(TableID="NSTimeSeriesCatalogTable",NewTableID="NSStationsTable",IncludeColumns="LocationId,StationNumId,StationName,StationRemoteTag",DistinctColumns="LocationId,StationNumId,StationName,StationRemoteTag")
#===============================================================================
# Join Campbell Cloud and NovaStar station tables.
#===============================================================================
CopyTable(TableID="CCStationsTable",NewTableID="JoinedStationsTable")
JoinTables(TableID="JoinedStationsTable",TableToJoinID="NSStationsTable",JoinColumns="StationId:StationRemoteTag",RowCountProperty="JoinedStationsTableRowCount")
SetPropertyFromTable(TableID="JoinedStationsTable",RowCountProperty="JoinedStationsTableRowCount")
If(Name="If_JoinedStationsTableEmpty",Condition="${JoinedStationsTableRowCount} == 0")
    SetProperty(PropertyName="HaveError",PropertyType="Boolean",PropertyValue="True")
    AppendFile(InputFile="${ProblemsFile}",AppendText="#  The joined Campbell Cloud and NovaStar stations table has zero rows.",OutputFile="${ProblemsFile}",IfNotFound="Ignore")
    SetProperty(PropertyName="ProblemCount",PropertyType="Integer",PropertyValue="${ProblemCount}",Add="1")
EndIf(Name="If_JoinedStationsTableEmpty")
If(Name="If_JoinedStationsTableWrongSize",Condition="${JoinedStationsTableRowCount} != ${CCStationsTableRowCount}")
    SetProperty(PropertyName="HaveError",PropertyType="Boolean",PropertyValue="True")
    AppendFile(InputFile="${ProblemsFile}",AppendText="#  The joined Campbell Cloud and NovaStar stations table row count (${JoinedStationsRableRowCount}) is different from the Campbell Cloud stations table row count (${CCStationsTableRowCount}).",OutputFile="${ProblemsFile}",IfNotFound="Ignore")
    SetProperty(PropertyName="ProblemCount",PropertyType="Integer",PropertyValue="${ProblemCount}",Add="1")
EndIf(Name="If_JoinedStationsTableWrongSize")
#===============================================================================
# Read Campbell Cloud time series.
#===============================================================================
# Loop through the stations to process:
# - read the Campbell Cloud data types in alphbetical order
# - check whether to read a data type based on the asset description
For(Name="For_Stations",IteratorProperty="StationId",TableID="JoinedStationsTable",TableColumn="StationId",TablePropertyMap="StationName:StationName,AssetDescription:AssetDescription")
    #
    # 'AssetDescription=ClimaVue station' have:
    #    Campbell Cloud           NovaStar
    #    -------------------------------------------------
    #    AirTemp                  TA - TempAir
    #    BattVoltage              VB - VoltageBattery
    #    BP                       PA - PressureAtmospheric
    #    Rain                     PP - Precip-Total.5Minute (5 minute totalized rain)
    #    RH                       XR - HumidityRelative
    #    Solar                    RW - RadiationSolarTotal
    #    WindDir                  UD - WindDir
    #    WindSpd                  US - WindSpeed
    #    WindSpdMax               UP - WindSpeedPeak
    If(Name="If_StationIs_ClimaVUE",Condition="${AssetDescription} == ClimaVUE station")
        ReadCampbellCloud(DataStore="CampbellCloud-SRBC",DataType="AirTemp",Interval="IrregSecond",StationId="${StationId}",Alias="%L-%T-%I")
        ReadCampbellCloud(DataStore="CampbellCloud-SRBC",DataType="BattVoltage",Interval="IrregSecond",StationId="${StationId}",Alias="%L-%T-%I")
        ReadCampbellCloud(DataStore="CampbellCloud-SRBC",DataType="BP",Interval="IrregSecond",StationId="${StationId}",Alias="%L-%T-%I")
        ReadCampbellCloud(DataStore="CampbellCloud-SRBC",DataType="Rain",Interval="IrregSecond",StationId="${StationId}",Alias="%L-%T-%I")
        ReadCampbellCloud(DataStore="CampbellCloud-SRBC",DataType="RH",Interval="IrregSecond",StationId="${StationId}",Alias="%L-%T-%I")
        ReadCampbellCloud(DataStore="CampbellCloud-SRBC",DataType="Solar",Interval="IrregSecond",StationId="${StationId}",Alias="%L-%T-%I")
        ReadCampbellCloud(DataStore="CampbellCloud-SRBC",DataType="WindDir",Interval="IrregSecond",StationId="${StationId}",Alias="%L-%T-%I")
        ReadCampbellCloud(DataStore="CampbellCloud-SRBC",DataType="WindSpd",Interval="IrregSecond",StationId="${StationId}",Alias="%L-%T-%I")
        ReadCampbellCloud(DataStore="CampbellCloud-SRBC",DataType="WindSpdMax",Interval="IrregSecond",StationId="${StationId}",Alias="%L-%T-%I")
    EndIf(Name="If_StationIs_ClimaVUE")
    #
    # 'AssetDescription=RainVue station' have:
    #    Campbell Cloud           NovaStar
    #    -------------------------------------------------
    #    BattVoltage              VB - VoltageBattery
    #    Rain                     PP - Precip-Total.5Minute (5 minute totalized rain)
    #    TotalAccumulation        PC - PrecipAccum   (accumulated rain amount, not tips)
    If(Name="If_StationIs_RainVUE",Condition="${AssetDescription} == RainVUE station")
        ReadCampbellCloud(DataStore="CampbellCloud-SRBC",DataType="BattVoltage",Interval="IrregSecond",StationId="${StationId}",Alias="%L-%T-%I")
        # ReadCampbellCloud(DataStore="CampbellCloud-SRBC",DataType="Rain",Interval="IrregSecond",StationId="${StationId}",Alias="%L-%T-%I")
        ReadCampbellCloud(DataStore="CampbellCloud-SRBC",DataType="TotalAccumulation",Interval="IrregSecond",StationId="${StationId}",Alias="%L-%T-%I")
    EndIf(Name="If_StationIs_RainVUE")
    #
    # 'AssetDescription=RangeVue station' have:
    #    Campbell Cloud           NovaStar
    #    -------------------------------------------------
    #    BattVoltage              VB - VoltageBattery
    #    Stage                    HG - WaterLevelRiver
    If(Name="If_StationIs_RangeVUE",Condition="${AssetDescription} == RangeVUE station")
        ReadCampbellCloud(DataStore="CampbellCloud-SRBC",DataType="BattVoltage",Interval="IrregSecond",StationId="${StationId}",Alias="%L-%T-%I")
        ReadCampbellCloud(DataStore="CampbellCloud-SRBC",DataType="Stage",Interval="IrregSecond",StationId="${StationId}",Alias="%L-%T-%I")
    EndIf(Name="If_StationIs_RangeVUE")
EndFor(Name="For_Stations")
#===============================================================================
# Post-process time series to set additional properties.
#===============================================================================
# Post-process time series to add additional properties:
# - look up the NovaStar data type (SHEF parmeter code) from the Campbell Cloud data type
# - look up the NovaStar station numid and set as a property
For(Name="For_Result_TimeSeries",IteratorProperty="TSID",IteratorValueProperty="${ts:tsid}",TSList="AllTS",TimeSeriesPropertyMap="DataType:CCDataType")
    # Get the properties from time series needed for lookups:
    # - datastream field
    # - station ID
    SetPropertyFromTimeSeries(TSList="AllMatchingTSID",TSID="${TSID}",PropertyName="DatastreamField",PropertyValue="${ts:datastream.field}")
    SetPropertyFromTimeSeries(TSList="AllMatchingTSID",TSID="${TSID}",PropertyName="StationId",PropertyValue="${ts:station.id}")
    # Lookup the NovaStar datatype from the datastream field.
    SetPropertyFromTable(TableID="CampbellCloudToNovaStarDataTypeTable",Column="NSDataType",ColumnIncludeFilters="CCDataType:${DatastreamField}",PropertyName="NSDataType")
    SetTimeSeriesProperty(TSList="AllMatchingTSID",TSID="${TSID}",PropertyName="novastar.datatype",PropertyType="String",PropertyValue="${NSDataType}")
    # Lookup the NovaStar stationNumId from the kpomed stations table.
    SetPropertyFromTable(TableID="JoinedStationsTable",Column="LocationId",ColumnIncludeFilters="StationId:${StationId}",PropertyName="NSLocId")
    SetTimeSeriesProperty(TSList="AllMatchingTSID",TSID="${TSID}",PropertyName="novastar.station.numid",PropertyType="String",PropertyValue="${NSLocId}")
EndFor(Name="For_Result_TimeSeries")
#===============================================================================
# Create/append to the problems file with the list of time series with no data.
#===============================================================================
# Use a file with the timestamp in the name to avoid conflicts:
# - make sure to remove after processing
SetProperty(PropertyName="MissingTSDataCount",PropertyType="Integer",PropertyValue="0")
For(Name="For_TS_Check",IteratorProperty="TSID",IteratorValueProperty="TSID",TSList="AllTS")
    # Message(Message="Checking time series ${TSID}.",CommandStatus="NOTIFICATION")
    If(Name="If_TS_Check",TSHasNoData="${TSID}")
        Message(Message="Time series ${TSID} has no data.",CommandStatus="NOTIFICATION")
        SetProperty(PropertyName="MissingTSDataCount",PropertyType="Integer",PropertyValue="${MissingTSDataCount}",Add="1")
        If(Name="If_MissingTS_CountFirst",Condition="${MissingTSDataCount} == 1")
            # Add an explanatory line in front of the first time series with missing data.
            AppendFile(InputFile="${ProblemsFile}",AppendText="#  The following time series have no data for the requested period:",OutputFile="${ProblemsFile}",IfNotFound="Ignore")
        EndIf(Name="If_MissingTS_CountFirst")
        AppendFile(InputFile="${ProblemsFile}",AppendText="#    ${TSID}",OutputFile="${ProblemsFile}",IfNotFound="Ignore")
        SetProperty(PropertyName="ProblemCount",PropertyType="Integer",PropertyValue="${ProblemCount}",Add="1")
    EndIf(Name="If_TS_Check")
EndFor(Name="For_TS_Check")
#===============================================================================
# Update the header template file so that it can be used as the output file header:
# - if there are problems so, say this in the header
# - if there are problems, include the problems in the file header
# - 'HeaderTemplateFile' = the template header file containing a placeholder for problems (not modified)
# - 'HeaderTempFile' = a temporary copy of 'HeaderTemplateFile' that is modified before using in the output file
#===============================================================================
SetProperty(PropertyName="HeaderTemplateFile",PropertyType="String",PropertyValue="download-campbell-cloud-data-header-template.txt")
SetProperty(PropertyName="HeaderTempFile",PropertyType="String",PropertyValue="${OutputFolder}/${DataLoaderTime}-SRBC-CampbellCloud-header.txt")
CopyFile(InputFile="${HeaderTemplateFile}",OutputFile="${HeaderTempFile}")
If(Name="If_ProblemCountNot0",Condition="${ProblemCount} == 0")
    TextEdit(InputFile="${HeaderTempFile}",SearchFor="INSERT_PROBLEMS_HERE",ReplaceWith="#  No problems were detected.",OutputFile="${HeaderTempFile}")
EndIf(Name="If_ProblemCountNot0")
If(Name="If_MissingTS_Count0",Condition="${MissingTSDataCount} > 0")
    TextEdit(InputFile="${HeaderTempFile}",SearchFor="INSERT_PROBLEMS_HERE",ReplaceWithFile="${ProblemsFile}",OutputFile="${HeaderTempFile}")
EndIf(Name="If_MissingTS_Count0")
#===============================================================================
# Create the NovaStar dataloader CSV file to load data.
#===============================================================================
# Create the output file for the NovaStar dataloader:
# - redundant 'DateTime' column is used to support old data loader
If(Name="If_Create_Dataloader_File",Condition="${DoCreateDataloaderFile} == True")
    CreateFolder(Folder="${DataloaderFolder}",CreateParentFolders="True",IfFolderExists="Ignore")
    # 'DateTime' not needed since NovaStar has been updated.
    WriteTimeSeriesToDataStream(OutputFile="${DataloaderFolder}/${DataLoaderTime}-SRBC-CampbellCloud.csv",OutputFileHeaderFile="${HeaderTempFile}",OutputLineFormat="${ts:station.id},${ts:datastream.field},${ts:novastar.station.numid},${ts:novastar.datatype},${tsdata:datetime},${tsdata:value}",DateTimeFormat="%Y-%m-%dT%H:%M:%S")
EndIf(Name="If_Create_Dataloader_File")
#===============================================================================
# Cleanup:
# - remove files with date in the filename so that they do not accumulate on the system
#===============================================================================
# RemoveFile(InputFile="${ProblemsFile}",IfNotFound="Ignore")
# RemoveFile(InputFile="${HeaderTempFile}",IfNotFound="Ignore")
